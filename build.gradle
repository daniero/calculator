buildscript {
    ext.antlr_version = '4.7.1'
    ext.kotlin_version = '1.2.41'
    ext.junit_version = '4.12'
    ext.cucumber_version = '1.2.5'

    repositories {
        mavenCentral()
    }
    dependencies {
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlin_version"
    }
}

group 'net.daniero'
version '1.0-SNAPSHOT'

apply plugin: 'antlr'
apply plugin: 'java'
apply plugin: 'kotlin'
apply plugin: 'idea'

repositories {
    mavenLocal()
    mavenCentral()
    jcenter()
}

dependencies {
    antlr "org.antlr:antlr4:$antlr_version"
    compile "org.antlr:antlr4-runtime:$antlr_version"
    compile "org.antlr:antlr4-runtime:$antlr_version"
    compile "org.jetbrains.kotlin:kotlin-stdlib-jdk8:$kotlin_version"

    testCompile "junit:junit:$junit_version"
    testCompile "info.cukes:cucumber-java8:$cucumber_version"
    testCompile "info.cukes:cucumber-junit:$cucumber_version"
}

def generatedSrcDir = 'generated-src'
def generatedJavaMain = "$generatedSrcDir/main/java"

sourceSets {
    generated {
        java.srcDir "$generatedSrcDir/main/java/"
    }
}

generateGrammarSource {
    def packageName = 'net.daniero.calculator.parser'
    def pathName = packageNameToPath(packageName)

    arguments += ['-package', packageName]
    outputDirectory = file("$generatedJavaMain/$pathName")
}

compileJava {
    source sourceSets.generated.java, sourceSets.main.java
}

compileKotlin {
    dependsOn generateGrammarSource
    source sourceSets.generated.java, sourceSets.main.kotlin
    kotlinOptions.jvmTarget = "1.8"
}

compileTestKotlin {
    kotlinOptions.jvmTarget = "1.8"
}

clean {
    delete generatedSrcDir
}

idea {
    module {
        generatedSourceDirs += file(generatedJavaMain)
    }
}

static def packageNameToPath(String packageName) {
    def dot = '.' as char
    def slash = '/' as char
    packageName.replace(dot, slash)
}
